group 'com.pragmatic'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'checkstyle'

sourceCompatibility = 1.9
targetCompatibility = 1.9
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.testng', name: 'testng', version: '6.14.3'
    compile group: 'io.appium', name: 'java-client', version: '6.0.0-BETA5'
}

// Force checks on build and test tasks.
build.dependsOn(check)
test.dependsOn(check)

// Do not run tests in check task (only run checkstyle).
check.dependsOn.remove(test)


task calcEmuAndroid6(type: Test, description: 'Calc Tests - Emulator (Android 6.0)', group: 'Verification') {

    systemProperty 'config', 'calc.emu.api23'

    useTestNG() {
        //report generation delegated to TestNG library:
        useDefaultListeners = true
        suites 'src/test/resources/suites/calc.xml'
    }

    //turn off Gradle's HTML report to avoid replacing the
    //reports generated by TestNG library:
    reports.html.enabled = false

    testLogging {
        showStandardStreams = false

        exceptionFormat "full"
        events "passed", "skipped", "failed"
    }
}

task calcSimIos11(type: Test, description: 'Calc Tests - iPhone 5s (iOS 11.3)', group: 'Verification') {
    systemProperty 'config', 'calc.sim.ios11'
    include "**/calc/tests/**"
    useTestNG() {
        useDefaultListeners = true
    }
    reports.html.enabled = false
    testLogging {
        showStandardStreams = false
        exceptionFormat "full"
        events "passed", "skipped", "failed"
    }
}